include <args.bare>
include <forms.bare>


async function emojiMain():
    # Parse arguments
    arguments = argsValidate(arrayNew( \
        objectNew('name', 'url', 'global', 'vURL', 'default', 'emoji.json', \
                  'description', 'The Emoji JSON configuration file URL'), \
        objectNew('name', 'category', \
                  'description', 'The selected emoji category name'), \
        objectNew('name', 'copied', 'type', 'int', 'explicit', true, \
                  'description', 'The most-recently copied emoji index'), \
        objectNew('name', 'config', 'type', 'bool', 'explicit', true, \
                  'description', 'If true, display the Emoji JSON configuration file format'), \
        objectNew('name', 'help', 'type', 'bool', 'explicit', true, \
                  'description', 'If true, display the Emoji application arguments documentation') \
    ))
    args = argsParse(arguments)
    url = objectGet(args, 'url')
    category = objectGet(args, 'category')
    copiedIndex = objectGet(args, 'copied')

    # Load the emojis
    emojiConfigJSON = systemFetch(url)
    emojiConfigObj = if(emojiConfigJSON, jsonParse(emojiConfigJSON))
    emojiConfig = if(emojiConfigObj, schemaValidate(emojiTypes, 'EmojiConfig', emojiConfigObj))
    if !emojiConfig:
        markdownPrint('**ERROR:** Failed to load "' + markdownEscape(url) + '"')
        return
    endif
    emojiCategories = objectGet(emojiConfig, 'categories')

    # Config file documentation?
    if objectGet(args, 'config'):
        # Render the menu
        markdownPrint('[Back](' + emojiAboutURL + ')')
        documentSetTitle('EmojiConfig')

        # Render the configuration schema documentation
        elementModelRender(schemaElements(emojiTypes, 'EmojiConfig'))
        return

    # Help?
    elif objectGet(args, 'help'):
        # Render the title
        title = 'Emoji Arguments'
        markdownPrint('[Back](' + emojiAboutURL + ')', '', '# ' + markdownEscape(title))
        documentSetTitle(title)

        # Render the arguments documention
        argsHelp(arguments)
        return

    # Category page
    elif category:
        # Render the title
        title = 'Emoji - ' + category
        markdownPrint(argsLink(arguments, 'Back', objectNew('category', null)), '', '# ' + markdownEscape(title))
        documentSetTitle(title)

        # Render the list of emoji
        elem = arrayNew()
        valign = 'vertical-align:middle;'
        for emoji, ixEmoji in arraySort(objectGet(emojiCategories, category), emojiCompare):
            emojiChar = objectGet(emoji, 'emoji')
            emojiName = objectGet(emoji, 'name')
            emojiStyle = valign + 'font-size:32pt; padding-right:0.25em; cursor: pointer; user-select: none; text-decoration: none;'
            copyCallback = systemPartial(emojiCopyClipboard, emojiChar, argsURL(arguments, objectNew('copied', ixEmoji)))
            arrayPush(elem, objectNew( \
                'html', 'p', \
                'elem', arrayNew( \
                    objectNew( \
                        'html', 'a', \
                        'attr', objectNew('style', emojiStyle), \
                        'elem', objectNew('text', emojiChar), \
                        'callback', objectNew('click', copyCallback) \
                    ), \
                    objectNew( \
                        'html', 'span', \
                        'attr', objectNew('style', valign), \
                        'elem', arrayNew( \
                            formsLinkButtonElements(emojiName, copyCallback), \
                            if(copiedIndex == ixEmoji, objectNew('text', ' (copied)')) \
                        ) \
                    ) \
                ) \
            ))
        endfor
        elementModelRender(elem)

    # Index page
    elif category == null:
        # Render the title
        title = 'Emoji'
        markdownPrint('[About](' + emojiAboutURL + ')', '', '# ' + markdownEscape(title))
        documentSetTitle(title)

        # Render the emoji category list
        categories = arraySort(objectKeys(emojiCategories))
        for category in categories:
            markdownPrint('', argsLink(arguments, category, objectNew('category', category)))
        endfor
    endif
endfunction


emojiAboutURL = 'https://craigahobbs.github.io/emoji/#url=README.md'


function emojiCompare(a, b):
    return systemCompare(objectGet(a, 'name'), objectGet(b, 'name'))
endfunction


async function emojiCopyClipboard(text, url):
    windowClipboardWrite(text)
    windowSetLocation(url)
endfunction


emojiTypes = schemaParse( \
    '# The Emoji JSON configuration file format', \
    'struct EmojiConfig', \
    '', \
    '    # The list of emoji categories' , \
    '    EmojiList{} categories', \
    '', \
    '', \
    '# A list of emoji', \
    'typedef Emoji[] EmojiList', \
    '', \
    '', \
    '# An emoji', \
    'struct Emoji', \
    '', \
    '    # The emoji character', \
    '    string emoji', \
    '', \
    '    # The emoji name', \
    '    string name' \
)
