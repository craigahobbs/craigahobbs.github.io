# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE

include <pager.mds>


# The money application main entry point
async function moneyMain(url):
    pagerModel = objectNew( \
        'pages', arrayNew( \
            objectNew('name', 'About', 'type', objectNew('markdown', objectNew( \
                'url', 'https://craigahobbs.github.io/money/README.md' \
            ))), \
            objectNew('name', 'Report', 'type', objectNew('function', objectNew( \
                'function', systemPartial(moneyPageReport, url) \
            ))), \
            objectNew('name', 'Schema', 'type', objectNew('function', objectNew( \
                'function', moneyPageSchema \
            ))) \
        ) \
    )
    pagerMain(pagerModel, objectNew('arguments', moneyArguments, 'hideNav', true, 'start', 'Report'))
endfunction


moneyArguments = argsValidate(arrayNew( \
    objectNew('name', 'page', 'default', 'Report'), \
    objectNew('name', 'url', 'global', 'vURL', 'default', 'money.json'), \
    objectNew('name', 'model', 'type', 'bool', 'default', false, 'explicit', true) \
))


async function moneyPageReport(url, args):
    url = if(url != null, url, objectGet(args, 'url'))

    # Load the money model
    moneyTextOrig = systemFetch(url, null, true)
    moneyText = stringReplace(moneyTextOrig, regexNew('$\\s+//.*$', 'mg'), '')
    moneyModel = schemaValidate(moneyTypes, "Money", jsonParse(moneyText))

    # Render the page title
    moneyName = objectGet(moneyModel, 'name')
    documentSetTitle('Money' + if(moneyName != null, ' - ' + moneyName, ''))
    markdownPrint('# ' + if(moneyName != null, moneyName, 'Money'), '')

    # Model loading error?
    if moneyModel == null:
        markdownPrint('**Error:** Failed to load "' + url + '"')
        return
    endif

    # Display the money model?
    if objectGet(args, 'model'):
        markdownPrint(argsLink(moneyArguments, "Hide Model"), '', '~~~', moneyTextOrig, '~~~')
        return
    else:
        markdownPrint(argsLink(moneyArguments, "Show Model", objectNew('model', true)))
    endif

    # Render the money report
    moneyReport(moneyModel)
endfunction


function moneyPageSchema():
    documentSetTitle('Money Schema')
    elementModelRender(schemaElements(moneyTypes, 'Money'))
endfunction


# Render the money report
function moneyReport(moneyModel):
    # Compute the scenario data
    data = moneyData(moneyModel)

    # Compute the account names
    accountNames = arrayNew()
    for account in objectGet(moneyModel, 'accounts'):
        arrayPush(accountNames, objectGet(account, 'name'))
    endfor

    # Render the line chart
    lineData = arrayNew()
    for row in data:
        for accountName in accountNames:
            arrayPush(lineData, objectNew( \
                'Date', objectGet(row, 'Date'), \
                'Account', accountName, \
                'Balance', objectGet(row, accountName) \
            ))
        endfor
    endfor
    dataLineChart(lineData, objectNew( \
        'width', 900, \
        'height', 300, \
        'precision', 2, \
        'datetime', 'day', \
        'x', 'Date', \
        'y', arrayNew('Balance'), \
        'color', 'Account', \
        'xTicks', objectNew('count', 5), \
        'yTicks', objectNew('count', 5), \
        'yLines', arrayNew(objectNew('value', 0)) \
    ))

    # Render the table
    fields = arrayNew('Date')
    formats = objectNew('Amount', objectNew('align', 'right'))
    for accountName in accountNames:
        arrayPush(fields, accountName)
        objectSet(formats, accountName, objectNew('align', 'right'))
    endfor
    arrayPush(fields, 'Amount', 'Description')
    dataTable(data, objectNew( \
        'datetime', 'day', \
        'fields', fields, \
        'formats', formats, \
        'precision', 2, \
        'trim', false \
    ))
endfunction


# Generate the money transaction data
function moneyData(moneyModel):
    beginDate = objectGet(moneyModel, 'beginDate')
    endDate = objectGet(moneyModel, 'endDate')

    # Create the initial account balances
    accountNames = arrayNew()
    initialRow = objectNew('Date', beginDate, 'Amount', 0, 'Description', '')
    data = arrayNew(initialRow)
    for account in objectGet(moneyModel, 'accounts'):
        accountName = objectGet(account, 'name')
        arrayPush(accountNames, accountName)
        objectSet(initialRow, accountName, objectGet(account, 'balance'))
    endfor

    # Add the transaction rows
    for txn in objectGet(moneyModel, 'transactions'):
        txnDate = objectGet(txn, 'date')
        txnAmount = objectGet(txn, 'amount')
        txnDesc = objectGet(txn, 'description')
        txnAccount = objectGet(txn, 'account')
        txnDest = objectGet(txn, 'destination')
        txnRepeat = objectGet(txn, 'repeat')
        txnRepeatPeriod = objectGet(txnRepeat, 'period')
        txnRepeatMultiple = objectGet(txnRepeat, 'multiple', 1)
        txnRepeatEndDate = objectGet(txnRepeat, 'endDate')

        # Add the transaction rows (as account deltas)
        while (txnRepeatEndDate == null || txnDate - txnRepeatEndDate <= 0) && txnDate - endDate <= 0:
            if txnDate - beginDate >= 0:
                # Add the transaction delta row
                txnRow = objectNew('Date', txnDate, txnAccount, txnAmount, 'Amount', txnAmount, 'Description', txnDesc)
                if txnDest != null:
                    objectSet(txnRow, txnDest, -txnAmount)
                endif
                arrayPush(data, txnRow)
            endif

            # Compute the next transaction date
            if txnRepeat == null:
                break
            elif txnRepeatPeriod == 'year':
                txnDate = datetimeNew(datetimeYear(txnDate) + txnRepeatMultiple, datetimeMonth(txnDate), datetimeDay(txnDate))
            elif txnRepeatPeriod == 'month':
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate) + txnRepeatMultiple, datetimeDay(txnDate))
            elif txnRepeatPeriod == 'week':
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate), datetimeDay(txnDate) + 7 * txnRepeatMultiple)
            else:
                # txnRepeatPeriod == 'day'
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate), datetimeDay(txnDate) + txnRepeatMultiple)
            endif
        endwhile
    endfor

    # Resolve the transaction rows
    dataSort(data, arrayNew(arrayNew('Date')))
    for txnRow, ixTxnRow in data:
        if ixTxnRow != 0:
            for txnAccount in accountNames:
                if txnAccount != 'Date':
                    balance = objectGet(arrayGet(data, ixTxnRow - 1), txnAccount) + objectGet(txnRow, txnAccount, 0)
                    objectSet(txnRow, txnAccount, balance)
                endif
            endfor
        endif
    endfor

    return data
endfunction


# The money schema
moneyTypes = schemaParse( \
    'group "Money"', \
    '', \
    '', \
    '# A money simulation model', \
    'struct Money', \
    '', \
    '    # The scenario name', \
    '    optional string name', \
    '', \
    '    # The start date', \
    '    date beginDate', \
    '', \
    '    # The end date', \
    '    date endDate', \
    '', \
    '    # The account list', \
    '    Account[len > 0] accounts', \
    '', \
    '    # The scheduled transactions', \
    '    Transaction[len > 0] transactions', \
    '', \
    '', \
    '# An account model', \
    'struct Account', \
    '', \
    '    # The account name', \
    '    string name', \
    '', \
    '    # The start balance', \
    '    float balance', \
    '', \
    '', \
    '# A transaction model', \
    'struct Transaction', \
    '', \
    '    # The transfer date', \
    '    date date', \
    '', \
    '    # The transfer amount', \
    '    float amount', \
    '', \
    '    # The transfer description', \
    '    string description', \
    '', \
    '    # The source account name', \
    '    string account', \
    '', \
    '    # The destination account name', \
    '    optional string destination', \
    '', \
    '    # The repeat model', \
    '    optional Repeat repeat', \
    '', \
    '', \
    '# A repeat model', \
    'struct Repeat', \
    '', \
    '    # The repeat period', \
    '    RepeatPeriod period', \
    '', \
    '    # The repeat period multiple (e.g. 2 months)', \
    '    optional int(> 0) multiple', \
    '', \
    '    # The end date', \
    '    optional date endDate', \
    '', \
    '', \
    '# A repeat period', \
    'enum RepeatPeriod', \
    '    year', \
    '    month', \
    '    week', \
    '    day' \
)
