# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE

include <pager.bare>
include <schemaDoc.bare>


# The money application main entry point
async function moneyMain(url):
    pagerModel = { \
        'pages': [ \
            {'name': 'About', 'type': {'markdown': { \
                'url': 'https://craigahobbs.github.io/money/README.md' \
            }}}, \
            {'name': 'Report', 'type': {'function': { \
                'function': systemPartial(moneyPageReport, url) \
            }}}, \
            {'name': 'Schema', 'type': {'function': { \
                'function': moneyPageSchema \
            }}} \
        ] \
    }
    pagerMain(pagerModel, {'arguments': moneyArguments, 'hideNav': true, 'start': 'Report'})
endfunction


moneyArguments = argsValidate([ \
    {'name': 'page', 'default': 'Report'}, \
    {'name': 'url', 'global': 'vURL', 'default': 'money.json'}, \
    {'name': 'model', 'type': 'bool', 'default': false, 'explicit': true} \
])


async function moneyPageReport(url, args):
    url = if(url != null, url, objectGet(args, 'url'))

    # Load the money model
    moneyTextOrig = systemFetch(url)
    moneyText = regexReplace(regexNew('$\\s+//.*$', 'm'), moneyTextOrig, '')
    moneyModel = schemaValidate(moneyTypes, "Money", jsonParse(moneyText))

    # Render the page title
    moneyName = objectGet(moneyModel, 'name')
    documentSetTitle('Money' + if(moneyName != null, ' - ' + moneyName, ''))
    markdownPrint('# ' + if(moneyName != null, moneyName, 'Money'), '')

    # Model loading error?
    if moneyModel == null:
        markdownPrint('**Error:** Failed to load "' + url + '"')
        return
    endif

    # Display the money model?
    if objectGet(args, 'model'):
        markdownPrint(argsLink(moneyArguments, "Hide Model"), '', '~~~', moneyTextOrig, '~~~')
        return
    else:
        markdownPrint(argsLink(moneyArguments, "Show Model", {'model': true}))
    endif

    # Render the money report
    moneyReport(moneyModel)
endfunction


function moneyPageSchema():
    documentSetTitle('Money Schema')
    markdownPrint(schemaDocMarkdown(moneyTypes, 'Money'))
endfunction


# Render the money report
function moneyReport(moneyModel):
    # Compute the scenario data
    data = moneyData(moneyModel)

    # Compute the account names
    accountNames = []
    for account in objectGet(moneyModel, 'accounts'):
        arrayPush(accountNames, objectGet(account, 'name'))
    endfor

    # Render the line chart
    lineData = []
    for row in data:
        for accountName in accountNames:
            arrayPush(lineData, { \
                'Date': objectGet(row, 'Date'), \
                'Account': accountName, \
                'Balance': objectGet(row, accountName) \
            })
        endfor
    endfor
    dataLineChart(lineData, { \
        'width': 900, \
        'height': 300, \
        'precision': 2, \
        'datetime': 'day', \
        'x': 'Date', \
        'y': ['Balance'], \
        'color': 'Account', \
        'xTicks': {'count': 5}, \
        'yTicks': {'count': 5}, \
        'yLines': [{'value': 0}] \
    })

    # Render the table
    fields = ['Date']
    formats = {'Amount': {'align': 'right'}}
    for accountName in accountNames:
        arrayPush(fields, accountName)
        objectSet(formats, accountName, {'align': 'right'})
    endfor
    arrayPush(fields, 'Amount', 'Description')
    dataTable(data, { \
        'datetime': 'day', \
        'fields': fields, \
        'formats': formats, \
        'precision': 2, \
        'trim': false \
    })
endfunction


# Generate the money transaction data
function moneyData(moneyModel):
    beginDate = objectGet(moneyModel, 'beginDate')
    endDate = objectGet(moneyModel, 'endDate')

    # Create the initial account balances
    accountNames = []
    initialRow = {'Date': beginDate, 'Amount': 0, 'Description': ''}
    data = [initialRow]
    for account in objectGet(moneyModel, 'accounts'):
        accountName = objectGet(account, 'name')
        arrayPush(accountNames, accountName)
        objectSet(initialRow, accountName, objectGet(account, 'balance'))
    endfor

    # Add the transaction rows
    for txn in objectGet(moneyModel, 'transactions'):
        if objectGet(txn, 'disabled', false):
            continue
        endif
        txnDate = objectGet(txn, 'date')
        txnAmount = objectGet(txn, 'amount')
        txnDesc = objectGet(txn, 'description')
        txnAccount = objectGet(txn, 'account')
        txnDest = objectGet(txn, 'destination')
        txnRepeat = objectGet(txn, 'repeat')
        txnRepeatPeriod = if(txnRepeat != null, objectGet(txnRepeat, 'period'))
        txnRepeatMultiple = if(txnRepeat != null, objectGet(txnRepeat, 'multiple', 1))
        txnRepeatEndDate = if(txnRepeat != null, objectGet(txnRepeat, 'endDate'))

        # Add the transaction rows (as account deltas)
        while (txnRepeatEndDate == null || txnDate - txnRepeatEndDate <= 0) && txnDate - endDate <= 0:
            if txnDate - beginDate >= 0:
                # Add the transaction delta row
                txnRow = {'Date': txnDate, txnAccount: txnAmount, 'Amount': txnAmount, 'Description': txnDesc}
                if txnDest != null:
                    objectSet(txnRow, txnDest, -txnAmount)
                endif
                arrayPush(data, txnRow)
            endif

            # Compute the next transaction date
            if txnRepeat == null:
                break
            elif txnRepeatPeriod == 'year':
                txnDate = datetimeNew(datetimeYear(txnDate) + txnRepeatMultiple, datetimeMonth(txnDate), datetimeDay(txnDate))
            elif txnRepeatPeriod == 'month':
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate) + txnRepeatMultiple, datetimeDay(txnDate))
            elif txnRepeatPeriod == 'week':
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate), datetimeDay(txnDate) + 7 * txnRepeatMultiple)
            else:
                # txnRepeatPeriod == 'day'
                txnDate = datetimeNew(datetimeYear(txnDate), datetimeMonth(txnDate), datetimeDay(txnDate) + txnRepeatMultiple)
            endif
        endwhile
    endfor

    # Resolve the transaction rows
    dataSort(data, [['Date']])
    for txnRow, ixTxnRow in data:
        if ixTxnRow != 0:
            for txnAccount in accountNames:
                balance = objectGet(arrayGet(data, ixTxnRow - 1), txnAccount) + objectGet(txnRow, txnAccount, 0)
                objectSet(txnRow, txnAccount, balance)
            endfor
        endif
    endfor

    return data
endfunction


# The money schema
moneyTypes = schemaParse( \
    'group "Money"', \
    '', \
    '', \
    '# A money simulation model', \
    'struct Money', \
    '', \
    '    # The scenario name', \
    '    optional string name', \
    '', \
    '    # The start date', \
    '    date beginDate', \
    '', \
    '    # The end date', \
    '    date endDate', \
    '', \
    '    # The account list', \
    '    Account[len > 0] accounts', \
    '', \
    '    # The scheduled transactions', \
    '    Transaction[len > 0] transactions', \
    '', \
    '', \
    '# An account model', \
    'struct Account', \
    '', \
    '    # The account name', \
    '    string name', \
    '', \
    '    # The start balance', \
    '    float balance', \
    '', \
    '', \
    '# A transaction model', \
    'struct Transaction', \
    '', \
    '    # The transfer date', \
    '    date date', \
    '', \
    '    # The transfer amount', \
    '    float amount', \
    '', \
    '    # The transfer description', \
    '    string description', \
    '', \
    '    # The source account name', \
    '    string account', \
    '', \
    '    # The destination account name', \
    '    optional string destination', \
    '', \
    '    # The repeat model', \
    '    optional Repeat repeat', \
    '', \
    '    # If true, the transaction is disabled (default is false)', \
    '    optional bool disabled', \
    '', \
    '', \
    '# A repeat model', \
    'struct Repeat', \
    '', \
    '    # The repeat period', \
    '    RepeatPeriod period', \
    '', \
    '    # The repeat period multiple (e.g. 2 months)', \
    '    optional int(> 0) multiple', \
    '', \
    '    # The end date', \
    '    optional date endDate', \
    '', \
    '', \
    '# A repeat period', \
    'enum RepeatPeriod', \
    '    year', \
    '    month', \
    '    week', \
    '    day' \
)
