# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE


async function downloadsMain(javascriptPackages, pythonPackages)
    # Menu
    markdownPrint('[Home](#var=)', '')
    downloadsPackageMenu(javascriptPackages, 'JavaScript')
    downloadsPackageMenu(pythonPackages, 'Python')
    if(vName, downloadsWindowMenu(arrayNew(7, 15, 21, 31, 45)))

    # Document title
    title = if(vType != null, vType, '') + ' Package Downloads' + if(vName != null, ' - ' + vName, '')
    markdownPrint('', '# ' + title)
    setDocumentTitle(title)

    # Package dashboard
    if(vName != null, downloadsDashboard(vName, vType), downloadsRankings(javascriptPackages, pythonPackages))
endfunction


function downloadsPackageMenu(packages, packageType)
    markdownPrint('**' + packageType + ':**')
    ixPackage = 0
    packageLoop:
        packageName = arrayGet(packages, ixPackage)
        if(ixPackage != 0, markdownPrint('**|**'))
        markdownPrint( \
            if(vName == packageName && vType == packageType, packageName, \
                '[' + packageName + '](' + downloadsLink(packageName, packageType) + ')') + \
            if(ixPackage == arrayLength(packages) - 1, '  ', '') \
        )
        ixPackage = ixPackage + 1
    jumpif (ixPackage < arrayLength(packages)) packageLoop
endfunction


function downloadsWindowMenu(windows)
    markdownPrint('**Window:**')
    ixWindow = 0
    windowLoop:
        window = arrayGet(windows, ixWindow)
        if(ixWindow != 0, markdownPrint('**|**'))
        markdownPrint(if(vWindow == window, window + ' days', '[' + window + ' days](' + downloadsLink(null, null, window) + ')'))
        ixWindow = ixWindow + 1
    jumpif (ixWindow < arrayLength(windows)) windowLoop
endfunction


function downloadsLink(name, type, window)
    name = if(name != null, name, vName)
    type = if(type != null, type, vType)
    window = if(window != null, window, vWindow)
    parts = arrayNew()
    if(name != null, arrayPush(parts, "var.vName='" + name + "'"))
    if(type != null, arrayPush(parts, "var.vType='" + type + "'"))
    if(window != null, arrayPush(parts, 'var.vWindow=' + window))
    return if(arrayLength(parts) == 0, '#var=', '#' + arrayJoin(parts, '&'))
endfunction


async function downloadsRankings(javascriptPackages, pythonPackages)
    # Compute the list of package raw data URLs
    dataURLs = arrayNew()
    dataInfo = arrayNew()
    packageLists = arrayNew(javascriptPackages, pythonPackages)
    packageTypes = arrayNew('JavaScript', 'Python')
    ixPackageList = 0
    packageListLoop:
        packageList = arrayGet(packageLists, ixPackageList)
        packageType = arrayGet(packageTypes, ixPackageList)
        ixPackage = 0
        packageLoop:
            packageName = arrayGet(packageList, ixPackage)

            # Add the package URL, etc.
            arrayPush(dataURLs, \
                if(packageType == 'Python', downloadsPythonDataURL(packageName), downloadsJavascriptDataURL(packageName)))
            arrayPush(dataInfo, objectNew('packageName', packageName, 'packageType', packageType))

            ixPackage = ixPackage + 1
        jumpif (ixPackage < arrayLength(packageList)) packageLoop
        ixPackageList = ixPackageList + 1
    jumpif (ixPackageList < arrayLength(packageLists)) packageListLoop

    # Fetch the package raw data and validate
    dataRaw = fetch(dataURLs)
    packageData = arrayNew()
    ixPackage = 0
    dataLoop:
        packageName = objectGet(arrayGet(dataInfo, ixPackage), 'packageName')
        packageType = objectGet(arrayGet(dataInfo, ixPackage), 'packageType')
        isDuplicate = arrayIndexOf(if(packageType == 'Python', javascriptPackages, pythonPackages), packageName) != -1
        displayName = packageName + if(isDuplicate, ' (' + packageType + ')', '')
        packageURL = if(packageType == 'Python', \
            'https://pypi.org/project/' + encodeURIComponent(packageName) + '/', \
            'https://www.npmjs.com/package/' + encodeURIComponent(packageName))

        # Validate the data
        data = if(packageType == 'Python', \
            downloadsPythonDataValidate(arrayGet(dataRaw, ixPackage), packageName), \
            downloadsJavascriptDataValidate(arrayGet(dataRaw, ixPackage), packageName))

        # Filter to only "with_mirrors"
        data = dataFilter(data, 'category == "with_mirrors"')

        # Compute the most recent month's downloads
        dataCalculatedField(data, 'year', 'year(date)')
        dataCalculatedField(data, 'month', 'month(date)')
        dataMonth = dataAggregate(data, 'downloads', 'sum', arrayNew('year', 'month'))
        dataSort(dataMonth, arrayNew(arrayNew('year', true), arrayNew('month', true)))
        arrayPush(packageData, objectNew( \
            'Package', '[' + displayName + '](' + packageURL + ')', \
            'Downloads', objectGet(arrayGet(dataMonth, 0), 'downloads') \
        ))

        ixPackage = ixPackage + 1
    jumpif (ixPackage < arrayLength(dataURLs)) dataLoop

    # Render the rankings chart
    dataSort(packageData, arrayNew(arrayNew('Downloads', true)))
    dataTable(packageData, objectNew('markdown', arrayNew('Package')))
endfunction


async function downloadsDashboard(packageName, packageType)
    chartWidth = 1000
    chartHeight = 250

    # Load and validate the package data
    data = if(packageType == 'Python', \
        downloadsPythonDataValidate(fetch(downloadsPythonDataURL(packageName)), packageName), \
        downloadsJavascriptDataValidate(fetch(downloadsJavascriptDataURL(packageName)), packageName))

    # Compute the with-mirrors three-week daily running average
    windowSize = if(vWindow != null, vWindow, 21)
    windowSizeHalf = (windowSize - 1) / 2
    ixDay = windowSizeHalf
    dataAvg = arrayNew()
    dataWithMirrors = dataFilter(data, 'category == "with_mirrors"')
    dataSort(dataWithMirrors, arrayNew(arrayNew('date')))
    dayLoop:
        avgSum = 0
        ixDayAvg = ixDay - windowSizeHalf
        dayAvgLoop:
            avgSum = avgSum + objectGet(arrayGet(dataWithMirrors, ixDayAvg), 'downloads')
            ixDayAvg = ixDayAvg + 1
        jumpif (ixDayAvg < ixDay + windowSizeHalf + 1) dayAvgLoop
        arrayPush(dataAvg, objectNew( \
            'date', objectGet(arrayGet(dataWithMirrors, ixDay), 'date'), \
            'downloads', avgSum / windowSize \
        ))
        ixDay = ixDay + 1
    jumpif (ixDay < arrayLength(dataWithMirrors) - windowSizeHalf) dayLoop

    # Render the daily running average chart
    dataLineChart(dataAvg, objectNew( \
        'title', 'Average Daily Downloads (' + windowSize + ' day window) - ' + packageName, \
        'width', chartWidth - mathFloor(1.5 * getTextWidth('with_mirrors', getTextHeight('', 0))), \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'ytick', objectNew('start', 0), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Render the daily line chart
    dataLineChart(data, objectNew( \
        'title', 'Daily Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'ytick', objectNew('start', 0), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Aggregate by month
    dataCalculatedField(data, 'month', 'dateFn(year(date), month(date), 1)', objectNew('dateFn', datetimeNew))
    dataMonth = dataAggregate(data, 'downloads', 'sum', arrayNew('month', 'category'))

    # Render the monthly line chart
    dataLineChart(dataMonth, objectNew( \
        'title', 'Monthly Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'month', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'ytick', objectNew('start', 0), \
        'datetime', 'month', \
        'precision', 0 \
    ))

    # Render the monthly table by most recent
    dataSort(dataMonth, arrayNew(arrayNew('month', true), arrayNew('category')))
    dataTable(dataMonth, objectNew( \
        'categories', arrayNew('month', 'category'), \
        'datetime', 'month' \
    ))
endfunction


function downloadsJavascriptDataURL(packageName)
    #today = datetimeToday()
    #yearAgo = datetimeNew(datetimeYear(today) - 1, datetimeMonth(today), datetimeDay(today))
    #return 'https://npm-stat.com/api/download-counts?package=' + package + \
    #    '&from=' + datetimeISOFormat(yearAgo, true) + '&until=' + datetimeISOFormat(today, true)
    return 'data/javascript-' + packageName + '.json'
endfunction


function downloadsJavascriptDataValidate(dataRaw, packageName)
    # Reshape the data
    data = arrayNew()
    downloadDates = objectGet(dataRaw, packageName)
    jumpif (arrayLength(downloadDates) == 0) dataDone
    dates = objectKeys(downloadDates)
    ixDate = 0
    dateLoop:
        date = arrayGet(dates, ixDate)
        arrayPush(data, objectNew( \
            'date', date, \
            'downloads', objectGet(downloadDates, date), \
            'category', 'with_mirrors' \
        ))
        ixDate = ixDate + 1
    jumpif (ixDate < arrayLength(dates)) dateLoop
    dataDone:

    # Validate the data
    return dataValidate(data)
endfunction


function downloadsPythonDataURL(packageName)
    #return 'https://pypistats.org/api/packages/' + packageName + '/overall'
    return 'data/python-' + packageName + '.json'
endfunction


function downloadsPythonDataValidate(dataRaw)
    return dataValidate(objectGet(dataRaw, 'data'))
endfunction
