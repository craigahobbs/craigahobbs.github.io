# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE


async function downloadsMain(javascriptPackages, pythonPackages)
    if vName != null then
        markdownPrint('[Rankings](#var=)', '')
        downloadsDashboard(vName, vType)
    else then
        downloadsRankings(javascriptPackages, pythonPackages)
    endif
endfunction


async function downloadsRankings(javascriptPackages, pythonPackages)
    # Document title
    title = 'Package Downloads'
    markdownPrint('', '# ' + title)
    setDocumentTitle(title)

    # Compute the list of package raw data URLs
    dataURLs = arrayNew()
    packages = arrayNew()
    packageLists = arrayNew(javascriptPackages, pythonPackages)
    packageTypes = arrayNew('JavaScript', 'Python')
    foreach packageType, ixPackageType in packageTypes do
        packageList = arrayGet(packageLists, ixPackageType)
        foreach packageName in packageList do
            if packageType == 'Python' then
                arrayPush(dataURLs, downloadsPythonDataURL(packageName))
            else then
                arrayPush(dataURLs, downloadsJavascriptDataURL(packageName))
            endif
            arrayPush(packages, objectNew('name', packageName, 'type', packageType))
        endforeach
    endforeach

    # Fetch the package raw data and validate
    dataRaw = fetch(dataURLs)
    packageData = arrayNew()
    foreach package, ixPackage in packages do
        packageName = objectGet(package, 'name')
        packageType = objectGet(package, 'type')

        # Validate the data
        if packageType == 'Python' then
            data = downloadsPythonDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://pypi.org/project/' + encodeURIComponent(packageName) + '/'
        else then
            data = downloadsJavascriptDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://www.npmjs.com/package/' + encodeURIComponent(packageName)
        endif

        # Filter to only 'with_mirrors'
        data = dataFilter(data, 'category == "with_mirrors"')

        # Compute the average monthly downloads
        dataCalculatedField(data, 'month', 'datetimeNew(year(date), month(date), 1)')
        monthly = dataAggregate(data, objectNew( \
            'categories', arrayNew('month'), \
            'measures', arrayNew( \
                objectNew('field', 'downloads', 'function', 'sum') \
            ) \
        ))
        monthlyAverage = dataAggregate(monthly, objectNew( \
            'measures', arrayNew( \
                objectNew('field', 'downloads', 'function', 'average') \
            ) \
        ))
        arrayPush(packageData, objectNew( \
            'Package', '[' + markdownEscape(packageName) + "](#var.vName='" + encodeURIComponent(packageName) + \
                "'&var.vType='" + encodeURIComponent(packageType) + "')", \
            'Language', '[' + markdownEscape(packageType) + '](' + packageURL + ')', \
            'Downloads', objectGet(arrayGet(monthlyAverage, 0), 'downloads') \
        ))
    endforeach

    # Render the rankings table
    dataSort(packageData, arrayNew(arrayNew('Downloads', true)))
    dataTable(packageData, objectNew( \
        'fields', arrayNew('Package', 'Language', 'Downloads'), \
        'markdown', arrayNew('Package', 'Language'), \
        'precision', 0 \
    ))
endfunction


async function downloadsDashboard(packageName, packageType)
    # Document title
    title = packageName + ' (' + packageType + ') Downloads'
    markdownPrint('', '# ' + markdownEscape(title))
    setDocumentTitle(title)

    # Load and validate the package data
    if packageType == 'Python' then
        data = downloadsPythonDataValidate(fetch(downloadsPythonDataURL(packageName)))
    else then
        data = downloadsJavascriptDataValidate(fetch(downloadsJavascriptDataURL(packageName)))
    endif

    # Aggregate by month
    dataCalculatedField(data, 'month', 'datetimeNew(year(date), month(date), 1)')
    monthly = dataAggregate(data, objectNew( \
        'categories', arrayNew('month', 'category'), \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'sum') \
        ) \
    ))
    today = datetimeToday()
    currentMonth = datetimeNew(datetimeYear(today), datetimeMonth(today), 1)
    monthlyComplete = dataFilter(monthly, 'month < currentMonth', objectNew('currentMonth', currentMonth))

    # Compute the average monthly downloads
    monthlyWithMirrors = dataFilter(monthly, 'category == "with_mirrors"')
    monthlyAverageData = dataAggregate(monthlyWithMirrors, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'average') \
        ) \
    ))
    monthlyAverage = mathRound(objectGet(arrayGet(monthlyAverageData, 0), 'downloads'))

    # Chart constants
    chartWidth = 1000
    chartHeight = 250

    # Render the monthly line chart
    dataLineChart(monthlyComplete, objectNew( \
        'title', 'Monthly Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'month', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'yTicks', objectNew('start', 0), \
        'yLines', arrayNew( \
            objectNew('value', monthlyAverage) \
        ), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Render the daily line chart
    dataLineChart(data, objectNew( \
        'title', 'Daily Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'yTicks', objectNew('start', 0), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Render the monthly table by most recent
    dataSort(monthly, arrayNew(arrayNew('month', true), arrayNew('category')))
    dataTable(monthly, objectNew( \
        'categories', arrayNew('month', 'category'), \
        'fields', arrayNew('downloads'), \
        'datetime', 'month' \
    ))
endfunction


function downloadsJavascriptDataURL(packageName)
    today = datetimeToday()
    yearAgo = datetimeNew(datetimeYear(today), datetimeMonth(today) - 12, datetimeDay(today))
    return 'https://api.npmjs.org/downloads/range/' + \
        datetimeISOFormat(yearAgo, true) + ':' + datetimeISOFormat(today, true) + '/' + packageName
endfunction


function downloadsJavascriptDataValidate(dataRaw)
    # Reshape the data
    data = arrayNew()
    downloads = objectGet(dataRaw, 'downloads')
    foreach download in downloads do
        arrayPush(data, objectNew( \
            'date', objectGet(download, 'day'), \
            'downloads', objectGet(download, 'downloads'), \
            'category', 'with_mirrors' \
        ))
    endforeach

    # Validate the data
    return dataValidate(data)
endfunction


function downloadsPythonDataURL(packageName)
    #return 'https://pypistats.org/api/packages/' + packageName + '/overall'
    return 'data/python-' + packageName + '.json'
endfunction


function downloadsPythonDataValidate(dataRaw)
    return dataValidate(objectGet(dataRaw, 'data'))
endfunction
