# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE


async function downloadsMain(javascriptPackages, pythonPackages)
    if vName != null then
        markdownPrint('[Rankings](#var=)', '')
        downloadsDashboard(vName, vType)
    else then
        downloadsRankings(javascriptPackages, pythonPackages)
    endif
endfunction


async function downloadsRankings(javascriptPackages, pythonPackages)
    # Document title
    title = 'Package Downloads'
    markdownPrint('', '# ' + markdownEscape(title))
    setDocumentTitle(title)

    # Compute the list of package raw data URLs
    dataURLs = arrayNew()
    packages = arrayNew()
    packageLists = arrayNew(javascriptPackages, pythonPackages)
    packageTypes = arrayNew('JavaScript', 'Python')
    foreach packageType, ixPackageType in packageTypes do
        packageList = arrayGet(packageLists, ixPackageType)
        foreach packageName in packageList do
            if packageType == 'Python' then
                arrayPush(dataURLs, downloadsPythonDataURL(packageName))
            else then
                arrayPush(dataURLs, downloadsJavascriptDataURL(packageName))
            endif
            arrayPush(packages, objectNew('name', packageName, 'type', packageType))
        endforeach
    endforeach

    # Fetch the package raw data and validate
    dataRaw = fetch(dataURLs)
    packageData = arrayNew()
    foreach package, ixPackage in packages do
        packageName = objectGet(package, 'name')
        packageType = objectGet(package, 'type')

        # Validate the data
        if packageType == 'Python' then
            data = downloadsPythonDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://pypi.org/project/' + encodeURIComponent(packageName) + '/'
        else then
            data = downloadsJavascriptDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://www.npmjs.com/package/' + encodeURIComponent(packageName)
        endif

        # Add the rankings data row
        arrayPush(packageData, objectNew( \
            'Package', '[' + markdownEscape(packageName) + "](#var.vName='" + encodeURIComponent(packageName) + \
                "'&var.vType='" + encodeURIComponent(packageType) + "')", \
            'Language', '[' + markdownEscape(packageType) + '](' + packageURL + ')', \
            'Downloads', objectGet(downloadsMonthlyData(data), 'monthlyAverage') \
        ))
    endforeach

    # Render the rankings table
    dataSort(packageData, arrayNew(arrayNew('Downloads', true)))
    dataTable(packageData, objectNew( \
        'fields', arrayNew('Package', 'Language', 'Downloads'), \
        'markdown', arrayNew('Package', 'Language'), \
        'precision', 0 \
    ))
endfunction


async function downloadsDashboard(packageName, packageType)
    # Document title
    title = packageName + ' (' + packageType + ') Downloads'
    markdownPrint('', '# ' + markdownEscape(title))
    setDocumentTitle(title)

    # Load and validate the package data
    if packageType == 'Python' then
        data = downloadsPythonDataValidate(fetch(downloadsPythonDataURL(packageName)))
    else then
        data = downloadsJavascriptDataValidate(fetch(downloadsJavascriptDataURL(packageName)))
    endif

    # Compute the monthly data and averages
    monthly = downloadsMonthlyData(data)
    dataMonthly = objectGet(monthly, 'dataMonthly')
    dataMonthlyComplete = objectGet(monthly, 'dataMonthlyComplete')

    # Chart constants
    chartWidth = 1000
    chartHeight = 250

    # Render the monthly line chart
    dataLineChart(dataMonthlyComplete, objectNew( \
        'title', 'Monthly Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'month', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'yTicks', objectNew('start', 0), \
        'yLines', arrayNew( \
            objectNew('value', objectGet(monthly, 'monthlyAverage')) \
        ), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Render the daily line chart
    dataLineChart(data, objectNew( \
        'title', 'Daily Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'color', 'category', \
        'yTicks', objectNew('start', 0), \
        'datetime', 'day', \
        'precision', 0 \
    ))

    # Render the monthly table by most recent
    dataSort(dataMonthly, arrayNew(arrayNew('month', true), arrayNew('category')))
    dataTable(dataMonthly, objectNew( \
        'categories', arrayNew('month', 'category'), \
        'fields', arrayNew('downloads', '%avg'), \
        'datetime', 'month' \
    ))
endfunction


function downloadsMonthlyData(data)
    # Add month/year calculated field
    dataCalculatedField(data, 'month', 'datetimeNew(year(date), month(date), 1)')

    # Compute the average monthly download data
    dataMonthly = dataAggregate(data, objectNew( \
        'categories', arrayNew('month', 'category'), \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'sum') \
        ) \
    ))

    # Exclude the first month of data if its incomplete
    dataMinDate = dataAggregate(data, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'date', 'function', 'min') \
        ) \
    ))
    minDate = objectGet(arrayGet(dataMinDate, 0), 'date')
    if datetimeDay(minDate) != 1 then
        dataMonthlyComplete = dataFilter(dataMonthly, 'datetimeYear(month) != minYear || datetimeMonth(month) != minMonth', \
            objectNew('minMonth', datetimeMonth(minDate), 'minYear', datetimeYear(minDate)))
    else then
        dataMonthlyComplete = dataMonthly
    endif

    # Exclude the current month of data
    dataMonthlyComplete = dataFilter(dataMonthlyComplete, 'datetimeYear(month) != curYear || datetimeMonth(month) != curMonth', \
        objectNew('curMonth', datetimeMonth(datetimeNow()), 'curYear', datetimeYear(datetimeNow())))

    # Compute the monthly average (excluding current month)
    dataWithMirrors = dataFilter(dataMonthlyComplete, "category == 'with_mirrors'")
    dataAverage = dataAggregate(dataWithMirrors, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'average') \
        ) \
    ))
    monthlyAverage = objectGet(arrayGet(dataAverage, 0), 'downloads')

    # Add the percentage-of-average field
    dataCalculatedField( \
        dataMonthly, \
        '%avg', \
        "if(category == 'with_mirrors', numberToFixed(100 * (downloads - monthlyAverage) / monthlyAverage, 1) + '%', '')", \
        objectNew('monthlyAverage', monthlyAverage) \
    )

    return objectNew( \
        'dataMonthly', dataMonthly, \
        'dataMonthlyComplete', dataMonthlyComplete, \
        'monthlyAverage', monthlyAverage \
    )
endfunction


function downloadsJavascriptDataURL(packageName)
    today = datetimeToday()
    yearAgo = datetimeNew(datetimeYear(today), datetimeMonth(today) - 12, 1)
    return 'https://api.npmjs.org/downloads/range/' + \
        datetimeISOFormat(yearAgo, true) + ':' + datetimeISOFormat(today, true) + '/' + packageName
endfunction


function downloadsJavascriptDataValidate(dataRaw)
    # Reshape the data
    data = arrayNew()
    downloads = objectGet(dataRaw, 'downloads')
    foreach download in downloads do
        arrayPush(data, objectNew( \
            'date', objectGet(download, 'day'), \
            'downloads', objectGet(download, 'downloads'), \
            'category', 'with_mirrors' \
        ))
    endforeach

    # Validate the data
    return dataValidate(data)
endfunction


function downloadsPythonDataURL(packageName)
    #return 'https://pypistats.org/api/packages/' + packageName + '/overall'
    return 'data/python-' + packageName + '.json'
endfunction


function downloadsPythonDataValidate(dataRaw)
    return dataValidate(objectGet(dataRaw, 'data'))
endfunction
