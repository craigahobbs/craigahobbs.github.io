# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE

include <args.mds>


async function downloadsMain(javascriptPackages, pythonPackages):
    args = argsParse(downloadsArguments)
    name = objectGet(args, 'name')
    type = objectGet(args, 'type')
    if name != null && type != null:
        downloadsDashboard(name, type)
    else:
        downloadsRankings(javascriptPackages, pythonPackages)
    endif
endfunction


downloadsArguments = argsValidate(arrayNew( \
    objectNew('name', 'name', 'explicit', true), \
    objectNew('name', 'type', 'explicit', true) \
))


async function downloadsRankings(javascriptPackages, pythonPackages):
    # Document title
    title = 'Package Downloads'
    markdownPrint('# ' + markdownEscape(title))
    documentSetTitle(title)

    # Compute the list of package raw data URLs
    dataURLs = arrayNew()
    packages = arrayNew()
    packageLists = arrayNew(javascriptPackages, pythonPackages)
    packageTypes = arrayNew('JavaScript', 'Python')
    for packageType, ixPackageType in packageTypes:
        packageList = arrayGet(packageLists, ixPackageType)
        for packageName in packageList:
            if packageType == 'Python':
                arrayPush(dataURLs, downloadsPythonDataURL(packageName))
            else:
                arrayPush(dataURLs, downloadsJavascriptDataURL(packageName))
            endif
            arrayPush(packages, objectNew('name', packageName, 'type', packageType))
        endfor
    endfor

    # Fetch the package raw data and validate
    dataRaw = systemFetch(dataURLs)
    packageData = arrayNew()
    for package, ixPackage in packages:
        packageName = objectGet(package, 'name')
        packageType = objectGet(package, 'type')

        # Validate the data
        if packageType == 'Python':
            data = downloadsPythonDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://pypi.org/project/' + urlEncodeComponent(packageName) + '/'
        else:
            data = downloadsJavascriptDataValidate(arrayGet(dataRaw, ixPackage))
            packageURL = 'https://www.npmjs.com/package/' + urlEncodeComponent(packageName)
        endif

        # Add the rankings data row
        arrayPush(packageData, objectNew( \
            'Package', argsLink(downloadsArguments, packageName, objectNew('name', packageName, 'type', packageType)), \
            'Language', '[' + markdownEscape(packageType) + '](' + packageURL + ')', \
            'Monthly Downloads', objectGet(downloadsMonthlyData(data), 'monthlyAverage') \
        ))
    endfor

    # Render the rankings table
    dataSort(packageData, arrayNew(arrayNew('Monthly Downloads', true)))
    dataTable(packageData, objectNew( \
        'fields', arrayNew('Package', 'Language', 'Monthly Downloads'), \
        'formats', objectNew( \
            'Package', objectNew('markdown', true), \
            'Language', objectNew('markdown', true) \
        ), \
        'precision', 0 \
    ))
endfunction


async function downloadsDashboard(packageName, packageType):
    # Document title
    title = packageName + ' (' + packageType + ') Downloads'
    markdownPrint( \
        argsLink(downloadsArguments, 'Rankings'), \
        '', \
        '# ' + markdownEscape(title) \
    )
    documentSetTitle(title)

    # Load and validate the package data
    if packageType == 'Python':
        data = downloadsPythonDataValidate(systemFetch(downloadsPythonDataURL(packageName)))
    else:
        data = downloadsJavascriptDataValidate(systemFetch(downloadsJavascriptDataURL(packageName)))
    endif

    # Compute the monthly data and averages
    monthly = downloadsMonthlyData(data)
    dataClean = objectGet(monthly, 'dataClean')
    dataMonthly = objectGet(monthly, 'dataMonthly')
    monthlyAverage = objectGet(monthly, 'monthlyAverage')

    # Chart constants
    chartWidth = 1000
    chartHeight = 250

    # Render the monthly line chart
    dataLineChart(dataMonthly, objectNew( \
        'title', 'Monthly Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'month', \
        'y', arrayNew('downloads'), \
        'yTicks', objectNew('start', 0), \
        'yLines', arrayNew( \
            objectNew('value', objectGet(monthly, 'monthlyAverage')) \
        ), \
        'datetime', 'day', \
        'precision', 1 \
    ))

    # Render the daily line chart (clean)
    dataLineChart(dataClean, objectNew( \
        'title', 'Daily Downloads (clean) - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'yTicks', objectNew('start', 0), \
        'yLines', arrayNew( \
            objectNew('value', objectGet(monthly, 'dailyAverageClean')) \
        ), \
        'datetime', 'day', \
        'precision', 1 \
    ))

    # Render the daily line chart
    dataLineChart(data, objectNew( \
        'title', 'Daily Downloads - ' + packageName, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'date', \
        'y', arrayNew('downloads'), \
        'yTicks', objectNew('start', 0), \
        'yLines', arrayNew( \
            objectNew('value', objectGet(monthly, 'dailyAverage')) \
        ), \
        'datetime', 'day', \
        'precision', 1 \
    ))

    # Render the monthly table by most recent
    dataSort(dataMonthly, arrayNew(arrayNew('month', true)))
    dataTable(dataMonthly, objectNew( \
        'fields', arrayNew('month', 'downloads', '%avg', 'partial'), \
        'formats', objectNew( \
            'downloads', objectNew('align', 'right') \
        ), \
        'datetime', 'month' \
    ))
endfunction


function downloadsMonthlyData(data):
    # Replace extreme outliers with a filtered average
    dataStddev = dataAggregate(data, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'name', 'downloadsStddev', 'function', 'stddev'), \
            objectNew('field', 'downloads', 'name', 'downloadsAverage', 'function', 'average') \
        ) \
    ))
    dailyAverage = objectGet(arrayGet(dataStddev, 0), 'downloadsAverage')
    downloadsMax = dailyAverage + 4 * objectGet(arrayGet(dataStddev, 0), 'downloadsStddev')
    dataClean = arrayNew()
    for row in data:
        rowClean = objectCopy(row)
        if objectGet(row, 'downloads') > downloadsMax:
            objectSet(rowClean, 'downloads', null)
        endif
        arrayPush(dataClean, rowClean)
    endfor
    dataCleanAverage = dataAggregate(dataClean, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'name', 'downloadsAverage', 'function', 'average') \
        ) \
    ))
    dailyAverageClean = objectGet(arrayGet(dataCleanAverage, 0), 'downloadsAverage')
    for rowClean in dataClean:
        if objectGet(rowClean, 'downloads') == null:
            objectSet(rowClean, 'downloads', dailyAverageClean)
        endif
    endfor

    # Compute the monthly download data
    dataCalculatedField(dataClean, 'month', 'datetimeNew(year(date), month(date), 1)')
    dataCalculatedField(dataClean, 'monthDay', 'datetimeDay(date)')
    dataMonthly = dataAggregate(dataClean, objectNew( \
        'categories', arrayNew('month'), \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'sum'), \
            objectNew('field', 'monthDay', 'name', 'monthDayMin', 'function', 'min'), \
            objectNew('field', 'monthDay', 'name', 'monthDayMax', 'function', 'max') \
        ) \
    ))
    dataCalculatedField(dataMonthly, 'monthCount', 'monthDayMax - monthDayMin + 1')

    # Adjust downloads for partial months
    dataMinMax = dataAggregate(dataClean, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'month', 'name', 'monthMin', 'function', 'min'), \
            objectNew('field', 'month', 'name', 'monthMax', 'function', 'max') \
        ) \
    ))
    dataMonthly = dataFilter(dataMonthly, \
        '(month - monthMin != 0 && month - monthMax != 0) || monthCount >= 5', \
        arrayGet(dataMinMax, 0) \
    )
    dataCalculatedField(dataMonthly, 'monthDays', 'arrayGet(monthDays, datetimeMonth(month) - 1)', objectNew( \
        'monthDays', arrayNew(31, 28, 31, 30, 31, 30, 31, 31, 30, 31, 30, 31) \
    ))
    dataCalculatedField(dataMonthly, 'partial', \
        "if((month - monthMin == 0 || month - monthMax == 0) && monthCount < monthDays, '*', '')", \
        arrayGet(dataMinMax, 0) \
    )
    dataCalculatedField(dataMonthly, 'downloads', \
        "if(partial == '', downloads, round(downloads * monthDays / monthCount, 0))", \
        arrayGet(dataMinMax, 0) \
    )

    # Filter leading zero-months and months with insufficient data
    dataSort(dataMonthly, arrayNew(arrayNew('month')))
    for row, ixRow in dataMonthly:
        if objectGet(row, 'downloads') != 0:
            dataMonthly = dataFilter(dataMonthly, 'month >= monthMin', objectNew('monthMin', objectGet(row, 'month')))
            break
        endif
    endfor

    # Compute the monthly average (excluding current month)
    dataAverage = dataAggregate(dataMonthly, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'downloads', 'function', 'average') \
        ) \
    ))
    monthlyAverage = objectGet(arrayGet(dataAverage, 0), 'downloads')

    # Add the percentage-of-average field
    dataCalculatedField( \
        dataMonthly, \
        '%avg', \
        "if(monthlyAverage, numberToFixed(100 * (downloads - monthlyAverage) / monthlyAverage, 1) + '%', '')", \
        objectNew('monthlyAverage', monthlyAverage) \
    )

    return objectNew( \
        'dataClean', dataClean, \
        'dataMonthly', dataMonthly, \
        'monthlyAverage', monthlyAverage, \
        'dailyAverage', dailyAverage, \
        'dailyAverageClean', dailyAverageClean \
    )
endfunction


function downloadsJavascriptDataURL(packageName):
    today = datetimeToday()
    yearAgo = datetimeNew(datetimeYear(today), datetimeMonth(today) - 12, 1)
    return 'https://api.npmjs.org/downloads/range/' + \
        datetimeISOFormat(yearAgo, true) + ':' + datetimeISOFormat(today, true) + '/' + packageName
endfunction


function downloadsJavascriptDataValidate(dataRaw):
    # Reshape the data
    data = arrayNew()
    for row in objectGet(dataRaw, 'downloads'):
        arrayPush(data, objectNew( \
            'date', objectGet(row, 'day'), \
            'downloads', objectGet(row, 'downloads') \
        ))
    endfor

    # Validate the data
    return dataValidate(data)
endfunction


function downloadsPythonDataURL(packageName):
    #return 'https://pypistats.org/api/packages/' + packageName + '/overall'
    return 'data/python-' + packageName + '.json'
endfunction


function downloadsPythonDataValidate(dataRaw):
    # Reshape the data
    data = arrayNew()
    for row in objectGet(dataRaw, 'data'):
        if objectGet(row, 'category') == 'without_mirrors':
            arrayPush(data, objectNew( \
                'date', objectGet(row, 'date'), \
                'downloads', objectGet(row, 'downloads') \
            ))
        endif
    endfor

    # Validate the data
    return dataValidate(data)
endfunction
