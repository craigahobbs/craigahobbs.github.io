# Licensed under the MIT License
# https://github.com/craigahobbs/craigahobbs.github.io/blob/main/LICENSE

include <args.bare>


async function downloadsMain(options):
    args = argsParse(downloadsArguments)
    page = objectGet(args, 'page')
    name = objectGet(args, 'name')
    language = objectGet(args, 'language')

    # Chart page?
    if page == 'chart':
        return downloadsChartPage(args, options)
    endif

    # Package dashboard?
    if name != null && language != null:
        return downloadsDashboard(args)
    endif

    # Render the index page
    downloadsIndex(args)
endfunction


downloadsArguments = argsValidate(arrayNew( \
    objectNew('name', 'page'), \
    objectNew('name', 'name'), \
    objectNew('name', 'language'), \
    objectNew('name', 'days', 'type', 'int', 'default', 90) \
))


async function downloadsIndex(args):
    trailingDays = objectGet(args, 'days')

    # Document title
    title = 'Package Downloads'
    markdownPrint( \
        '# ' + markdownEscape(title), \
        '', \
        argsLink(downloadsArguments, 'Time Chart', objectNew('page', 'chart')) \
    )
    documentSetTitle(title)

    # Load the package data
    data = dataValidate(jsonParse(systemFetch('downloads.json')), true)
    dataAverage = downloadsDataAverage(data, trailingDays, true)

    # Create links and compute total downloads
    totalDownloads = 0
    for row in dataAverage:
        rowPackage = objectGet(row, 'Package')
        rowLanguage = objectGet(row, 'Language')
        totalDownloads = totalDownloads + objectGet(row, 'Downloads')

        # Package page link
        objectSet(row, 'Package', argsLink(downloadsArguments, rowPackage, objectNew('name', rowPackage, 'language', rowLanguage)))

        # Package repository link
        if rowLanguage == 'Python':
            packageURL = 'https://pypi.org/project/' + urlEncodeComponent(rowPackage) + '/'
        else:
            packageURL = 'https://www.npmjs.com/package/' + urlEncodeComponent(rowPackage)
        endif
        objectSet(row, 'Language', '[' + markdownEscape(rowLanguage) + '](' + packageURL + ')')
    endfor
    dataSort(dataAverage, arrayNew(arrayNew('Downloads', true)))
    arrayPush(dataAverage, objectNew('Package', '**Total**', 'Language', '', 'Downloads', totalDownloads))

    # Render the package downloads table
    dataTable(dataAverage, objectNew( \
        'fields', arrayNew('Package', 'Language', 'Downloads'), \
        'formats', objectNew( \
            'Package', objectNew('markdown', true), \
            'Language', objectNew('markdown', true), \
            'Downloads', objectNew('align', 'right', 'header', 'Monthly Downloads') \
        ), \
        'precision', 0 \
    ))
endfunction


async function downloadsChartPage(args, options):
    trailingDays = objectGet(args, 'days')

    # Render the title
    title = 'Downloads Chart'
    documentSetTitle(title)
    markdownPrint( \
        argsLink(downloadsArguments, 'Back', objectNew('page', null)), \
        '', \
        '# ' + markdownEscape(title) \
    )

    # Compute the color legend order
    data = downloadsDataLoad()
    dataAverage = downloadsDataAverage(data, trailingDays)
    dataSort(dataAverage, arrayNew(arrayNew('Downloads', true)))
    colorOrder = arrayNew()
    for row in dataAverage:
        arrayPush(colorOrder, objectGet(row, 'Unique'))
    endfor

    # Add the featured column
    featuredMap = objectGet(options, 'featured') || objectNew()
    dataCalculatedField( \
        data, \
        'Featured', \
        'objectGet(objectGet(featuredMap, Package), Language, false)', \
        objectNew('featuredMap', featuredMap) \
    )

    # First featured, then non-featured
    for isFeatured in arrayNew(true, false):
        # Create the chart
        dataMonthly = downloadsDataMonthly(dataFilter(data, '!!Featured == isFeatured', objectNew('isFeatured', isFeatured)))
        if !dataMonthly:
            continue
        endif
        chartWidth = mathFloor(mathMax(600, windowWidth() - 4 * documentFontSize()))
        chartHeight = mathFloor(0.3 * chartWidth)
        dataLineChart(dataMonthly, objectNew( \
            'title', 'Monthly Downloads' + if(isFeatured, ' (Featured)', ''), \
            'width', chartWidth, \
            'height', chartHeight, \
            'x', 'Month', \
            'y', arrayNew('Downloads'), \
            'color', 'Unique', \
            'colorOrder', colorOrder, \
            'yTicks', objectNew('start', 0, 'count', 5), \
            'precision', 0, \
            'datetime', 'day' \
        ))
    endfor

    # Redraw on resize
    windowSetResize(systemPartial(downloadsChartPage, args))
endfunction


async function downloadsDashboard(args):
    name = objectGet(args, 'name')
    language = objectGet(args, 'language')
    trailingDays = objectGet(args, 'days')

    # Document title
    title = name + ' (' + language + ') Downloads'
    markdownPrint( \
        argsLink(downloadsArguments, 'Back', objectNew('name', null, 'language', null)), \
        '', \
        '# ' + markdownEscape(title) \
    )
    documentSetTitle(title)

    # Load the package data
    data = downloadsDataLoad()
    dataMonthly = downloadsDataMonthly(data)
    monthlyAverageData = downloadsDataAverage(data, trailingDays, true)

    # Filter to this package's data
    filterExpr = 'Package == packageName && Language == packageLanguage'
    filterVars = objectNew('packageName', name, 'packageLanguage', language)
    data = dataFilter(data, filterExpr, filterVars)
    dataMonthly = dataFilter(dataMonthly, filterExpr, filterVars)
    monthlyAverageData = dataFilter(monthlyAverageData, filterExpr, filterVars)
    monthlyAverage = objectGet(arrayGet(monthlyAverageData, 0), 'Downloads')

    # Compute the minimum date
    minDateData = dataAggregate(data, objectNew( \
        'measures', arrayNew( \
            objectNew('field', 'Date', 'function', 'min') \
        ) \
    ))
    minDateMin = objectGet(arrayGet(minDateData, 0), 'Date')
    minDate = datetimeNew(datetimeYear(minDateMin), datetimeMonth(minDateMin), datetimeDay(minDateMin) + trailingDays)

    # Compute the daily trailing average
    for row in data:
        rowDate = objectGet(row, 'Date')
        if rowDate >= minDate:
            averageDownloadsData = downloadsDataAverage(data, trailingDays, false, rowDate)
            averageDownloads = objectGet(arrayGet(averageDownloadsData, 0), 'Downloads')
            objectSet(row, 'Average Downloads', averageDownloads)
        endif
    endfor

    # Render the monthly line chart
    chartWidth = mathFloor(mathMax(600, windowWidth() - 4 * documentFontSize()))
    chartHeight = mathFloor(mathMax(200, mathFloor(0.2 * chartWidth)))
    dataLineChart(dataMonthly, objectNew( \
        'title', 'Monthly Downloads - ' + name, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'Month', \
        'y', arrayNew('Downloads'), \
        'yTicks', objectNew('start', 0), \
        'xLines', arrayNew( \
            objectNew('value', downloadsDataAverageMinDate(dataMonthly, trailingDays, 'Month'), 'label', '') \
        ), \
        'yLines', arrayNew( \
            objectNew('value', monthlyAverage) \
        ), \
        'datetime', 'day', \
        'precision', 1 \
    ))

    # Render the daily line chart
    dataLineChart(data, objectNew( \
        'title', 'Daily Downloads - ' + name, \
        'width', chartWidth, \
        'height', chartHeight, \
        'x', 'Date', \
        'y', arrayNew('Downloads', 'Average Downloads'), \
        'yTicks', objectNew('start', 0), \
        'xLines', arrayNew( \
            objectNew('value', downloadsDataAverageMinDate(data, trailingDays), 'label', '') \
        ), \
        'yLines', arrayNew( \
            objectNew('value', averageDownloads) \
        ), \
        'datetime', 'day', \
        'precision', 1 \
    ))

    # Render the monthly table by most recent
    dataSort(dataMonthly, arrayNew(arrayNew('Month', true)))
    dataCalculatedField( \
        dataMonthly, \
        '% Average', 'numberToFixed((100 * (Downloads - monthlyAverage) / monthlyAverage), 1, true) + "%"', \
        objectNew('monthlyAverage', monthlyAverage) \
    )
    dataTable(dataMonthly, objectNew( \
        'fields', arrayNew('Month', 'Downloads', '% Average'), \
        'formats', objectNew( \
            'Downloads', objectNew('align', 'right') \
        ), \
        'datetime', 'month', \
        'precision', 0 \
    ))

    # Redraw on resize
    windowSetResize(systemPartial(downloadsDashboard, args))
endfunction


async function downloadsDataLoad():
    data = dataValidate(jsonParse(systemFetch('downloads.json')), true)

    # Compute the unique package names
    nameLanguages = objectNew()
    for row in data:
        name = objectGet(row, 'Package')
        language = objectGet(row, 'Language')
        languages = objectGet(nameLanguages, name)
        if !languages:
            objectSet(nameLanguages, name, objectNew(language, true))
        else:
            objectSet(languages, language, true)
        endif
    endfor
    for row in data:
        name = objectGet(row, 'Package')
        language = objectGet(row, 'Language')
        if arrayLength(objectKeys(objectGet(nameLanguages, name))) == 1:
            objectSet(row, 'Unique', name)
        elif language == 'Python':
            objectSet(row, 'Unique', name + ' (py)')
        else:
            objectSet(row, 'Unique', name + ' (js)')
        endif
    endfor

    return data
endfunction


function downloadsDataMonthly(data):
    # Filter-out the current month
    today = datetimeToday()
    maxDate = datetimeNew(datetimeYear(today), datetimeMonth(today), 1)
    dataFiltered = dataFilter(data, 'Date < maxDate', objectNew('maxDate', maxDate))

    # Compute the month field
    dataCalculatedField(dataFiltered, 'Month', 'datetimeISOParse(stringSlice(datetimeISOFormat(Date, true), 0, 7) + "-01")')

    # Aggregate by month
    return dataAggregate(dataFiltered, objectNew( \
        'categories', arrayNew('Language', 'Package', 'Unique', 'Month'), \
        'measures', arrayNew( \
            objectNew( \
                'field', 'Downloads', \
                'function', 'sum' \
            ) \
        ) \
    ))
endfunction


function downloadsDataAverage(data, trailingDays, monthly, maxDate):
    # Filter the data
    minDate = downloadsDataAverageMinDate(data, trailingDays, null, maxDate)
    if maxDate:
        dataFiltered = dataFilter(data, 'Date >= minDate && Date <= maxDate', objectNew('minDate', minDate, 'maxDate', maxDate))
    else:
        dataFiltered = dataFilter(data, 'Date >= minDate', objectNew('minDate', minDate))
    endif

    # Compute the average
    dataAverage = dataAggregate(dataFiltered, objectNew( \
        'categories', arrayNew('Language', 'Package', 'Unique'), \
        'measures', arrayNew( \
            objectNew('field', 'Downloads', 'function', 'sum') \
        ) \
    ))
    dataCalculatedField(dataAverage, 'Downloads', 'Downloads / trailingDays', objectNew('trailingDays', trailingDays))

    # Monthly average?
    if monthly:
        dataCalculatedField(dataAverage, 'Downloads', 'Downloads * 30')
    endif

    return dataAverage
endfunction


function downloadsDataAverageMinDate(data, trailingDays, field, maxDate):
    field = if(field, field, 'Date')
    if !maxDate:
        maxDateData = dataAggregate(data, objectNew( \
            'measures', arrayNew( \
                objectNew('field', field, 'function', 'max') \
            ) \
        ))
        maxDate = objectGet(arrayGet(maxDateData, 0), field)
    endif
    return datetimeNew(datetimeYear(maxDate), datetimeMonth(maxDate), datetimeDay(maxDate) - trailingDays)
endfunction
