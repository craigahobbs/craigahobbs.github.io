# Licensed under the MIT License
# https://github.com/craigahobbs/bare-script/blob/main/LICENSE


include <args.bare>
include <elementModel.bare>
include <forms.bare>
include <qrcode.bare>


# QR code application main entry point
function qrcodeAppMain():
    # Parse arguments
    args = argsParse(qrcodeAppArguments)
    copied = objectGet(args, 'copied')
    level = objectGet(args, 'level')
    message = objectGet(args, 'message')
    size = mathMax(50, mathMin(1000, objectGet(args, 'size')))

    # Render the title
    title = 'QR Code'
    documentSetTitle(title)
    markdownPrint('# ' + markdownEscape(title))

    # Generate the QR code matrix and SVG text
    matrix = qrcodeMatrix(message, level)
    svgText = elementModelToString(qrcodeElements(matrix, size, level))
    svgURL = urlObjectCreate(svgText, 'image/svg+xml')
    svgFilename = 'qrcode.svg'

    # Render the text input
    documentSetFocus(qrcodeAppMessageId)
    elementModelRender([ \
        {'html': 'p', 'elem': {'html': 'b', 'elem': {'text': 'Message:'}}}, \
        {'html': 'p', 'elem': formsTextElements(qrcodeAppMessageId, message, 35, qrcodeAppOnMessage)}, \
        {'html': 'p', 'elem': [ \
            {'html': 'b', 'elem': {'text': 'Level: '}}, \
            formsLinkElements('Low', if(level != 'low', argsURL(qrcodeAppArguments, {'level': 'low'}))), \
            {'text': ' | '}, \
            formsLinkElements('Medium', if(level != 'medium', argsURL(qrcodeAppArguments, {'level': 'medium'}))), \
            {'text': ' | '}, \
            formsLinkElements('Quartile', if(level != 'quartile', argsURL(qrcodeAppArguments, {'level': 'quartile'}))), \
            {'text': ' | '}, \
            formsLinkElements('High', if(level != 'high', argsURL(qrcodeAppArguments, {'level': 'high'}))) \
        ]}, \
        {'html': 'p', 'elem': [ \
            {'html': 'b', 'elem': {'text': 'Size: '}}, \
            formsLinkElements('Smaller', argsURL(qrcodeAppArguments, {'size': size - 50})), \
            {'text': ' | '}, \
            formsLinkElements('Bigger', argsURL(qrcodeAppArguments, {'size': size + 50})) \
        ]}, \
        {'html': 'p', 'elem': [ \
            formsLinkButtonElements('Update', qrcodeAppOnMessage), \
            {'text': ' | '}, \
            formsLinkElements('Reset', argsURL(qrcodeAppArguments, null, true)), \
            {'text': ' | '}, \
            {'html': 'a', 'attr': {'href': svgURL, 'download': svgFilename}, 'elem': {'text': 'Download SVG'}}, \
            {'text': ' | '}, \
            formsLinkButtonElements('Copy SVG', systemPartial(qrcodeAppOnCopySVG, svgText)), \
            if(copied, {'text': ' (copied)'}) \
        ]} \
    ])

    # Draw the QR code
    drawNew(size, size)
    qrcodeDraw(matrix, 0, 0, size, level)
endfunction


# The QR code application arguments
qrcodeAppArguments = argsValidate([ \
    {'name': 'copied', 'type': 'bool', 'default': false, 'explicit': true}, \
    {'name': 'level', 'default': 'low'}, \
    {'name': 'message', 'default': 'https://craigahobbs.github.io/'}, \
    {'name': 'size', 'type': 'int', 'default': 300} \
])


# Message ID
qrcodeAppMessageId = 'message'


# Message on-click
function qrcodeAppOnMessage():
    message = documentInputValue(qrcodeAppMessageId)
    windowSetLocation(argsURL(qrcodeAppArguments, {'message': message}))
endfunction


# Copy SVG on-click
async function qrcodeAppOnCopySVG(svgText):
    windowClipboardWrite(svgText, 'image/svg+xml')
    windowSetLocation(argsURL(qrcodeAppArguments, {'copied': true}))
endfunction
